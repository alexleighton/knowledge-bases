open Stdint

module CE = Control.Exception

type t = Uuidm.t

(* UUIDv7 layout: 48-bit timestamp + 0111 version + 12-bit rand + 10 variant + 62-bit rand *)
let ver = Uint128.of_string "0b0111"
let var = Uint128.of_string "0b10"

let get_timestamp_ms () = Unix.gettimeofday () *. 1000. |> Uint48.of_float

let make () =
  let result = Uint128.of_int 0 in
  let ts = get_timestamp_ms () in
  (* 48 bits of timestamp *)
  let result = Uint128.logor result (Uint128.shift_left (Uint128.of_uint48 ts) (128 - 48)) in
  (* 4 bits of version *)
  let result = Uint128.logor result (Uint128.shift_left ver (128 - 48 - 4)) in
  (* 12 bits of random *)
  let bits_randoms_12_i32 = Int32.shift_right_logical (Random.bits32 ()) 20 in
  let bit_randoms_12_uint128 = Uint128.of_int32 bits_randoms_12_i32 in
  let result =
    Uint128.logor result
 (Uint128.shift_left bit_randoms_12_uint128 (128 - 48 - 4 - 12))
  in
  (* 2 bits of variant *)
  let result = Uint128.logor result (Uint128.shift_left var (128 - 48 - 4 - 12 - 2)) in
  (* 62 bits of random *)
  let randomi64 = Int64.shift_right_logical (Random.bits64 ()) 2 in
  let randomuint64 = Uint128.of_int64 randomi64 in
  let result = Uint128.logor result randomuint64 in
  (* Convert Uint128 to Uuidm.t *)
  let bytes = Bytes.make 16 '\000' in
  Uint128.to_bytes_big_endian result bytes 0;
  match Uuidm.of_binary_string (Bytes.to_string bytes) with
  | Some uuid -> uuid
  | None -> failwith "Failed to create UUID from generated bytes"

let to_uint128 uuid =
  let uuid_bytes = Bytes.of_string (Uuidm.to_binary_string uuid) in
  Uint128.of_bytes_big_endian uuid_bytes 0

let of_uint128 uint128 =
  let bytes = Bytes.make 16 '\000' in
  Uint128.to_bytes_big_endian uint128 bytes 0;
  match Uuidm.of_binary_string (Bytes.to_string bytes) with
  | Some uuid -> uuid
  | None -> invalid_arg "Invalid uint128 for UUID"

let to_uuidm (uuid : t) : Uuidm.t = uuid

let of_uuidm (uuid : Uuidm.t) : t = uuid

let to_string uuid = Uuidm.to_string uuid

let of_string s =
  let s = String.lowercase_ascii s in
  (* Remove hyphens and collect hex characters *)
  let buf = Buffer.create (String.length s) in
  String.iter (fun c -> if c <> '-' then Buffer.add_char buf c) s;
  let hex = Buffer.contents buf in
  (* Validate length *)
  let len = String.length hex in
  if len <> 32 then CE.invalid_arg2 "Invalid UUID hex length: expected %d, got %d" 32 len;
  (* Validate characters - hex digits: 0-9, a-f *)
  (* let is_hex c = (c >= '0' && c <= '9') || (c >= 'a' && c <= 'f') in *)
  String.iter (fun c ->
    if not (Char.is_hex_digit c) then
      CE.invalid_arg1 "Invalid hex character '%c' in UUID string" c
  ) hex;
  (* Parse the hex string into a Uint128 and convert to UUID *)
  let uint128 = Uint128.of_string ("0x" ^ hex) in
  of_uint128 uint128
